buildscript{
    repositories{
        jcenter()
    }
    dependencies{
        classpath "me.tatarka:gradle-retrolambda:$retrolambda"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'

    }
}

apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'uk.co.sentinelweb'
version = '0.1'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


repositories {
    jcenter()
    maven {
        url "https://dl.bintray.com/sentinelweb/generic/"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //compile project(':microserver-java')
    compile 'uk.co.sentinelweb:microserver-java:0.1'

    compile "io.reactivex:rxjava:$rxjava_version"
    // https://github.com/ReactiveX/RxAndroid
    compile 'io.reactivex:rxandroid:1.2.0'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:$android_support"
    testCompile 'junit:junit:4.12'
}

def siteUrl = 'https://github.com/sentinelweb/microserver'
def gitUrl = 'https://github.com/sentinelweb/microserver'

bintray {
    user = SW_JCENTER_USER
    key = SW_JCENTER_API_KEY

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'generic'
        name = 'microserver-android'
        desc = 'Microserver service for android'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/sentinelweb/microserver/issues'
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['aar', 'android', 'microserver']
        publicDownloadNumbers = true
        version {
            name = '0.1'
            desc = 'Microserver utils for Android'
            vcsTag = '0.1'
            attributes = ['gradle-plugin': 'uk.co.sentinelweb:microserver-android:0.1']
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'Microserver service for android'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'sentinelweb'
                        name 'Robert Munro'
                        email 'rrmunro@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}


